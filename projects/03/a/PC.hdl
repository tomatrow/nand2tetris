// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16 (in = lastOut, out = lastOutPlusOne);

    Mux16 (sel = inc, a = lastOut, b = lastOutPlusOne, out = incOut);
    Mux16 (sel = load, a = incOut, b = in, out = loadOut);
    Mux16 (sel = reset, a = loadOut, b = false, out = resetOut);
    
    Register (in = resetOut, load = true, out = lastOut, out = out);
}
